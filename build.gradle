buildscript {
    ext {
        compose_version = '1.2.0-alpha08'
        ktor_version = '2.0.1'
        koin_version = '3.1.6'
        lifecycle_version = '2.4.1'
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.6.10"
        classpath 'com.squareup.sqldelight:gradle-plugin:1.5.3'
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.1.2' apply false
    id 'com.android.library' version '7.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.20' apply false
}

// src - https://chris.banes.dev/composable-metrics/
// https://www.jetpackcompose.app/articles/how-can-I-debug-recompositions-in-jetpack-compose
// usage
//  ./gradlew assembleRelease -Pmyapp.enableComposeCompilerReports=true --rerun-tasks
subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("myapp.enableComposeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}